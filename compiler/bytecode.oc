import std::vector::{ Vector }
import std::compact_map::{ Map }
import std::span::{ Span }
import std::buffer::{ Buffer }
import std::mem

import @vm::value::{ Value, String, Object }
import @vm::{ VM }

enum OpCode {
    Null
    True
    False
    Constant
    SetGlobal
    SetLocal
    GetGlobal
    GetLocal
    Call
    Pop
    Add
    Sub
    Mul
    Div
    Return
    JumpIfFalse
    Jump
    Loop
    LessThan
    GreaterThan
    Equal
    Halt
    CloseFunction
    SetUpvalue
    GetUpvalue
    CloseUpvalue
    Class
    SetMember
    GetMember
    Method
}

struct DebugLocRun {
    span: Span
    count: u32
}

struct Chunk {
    start_span: Span
    code: Buffer
    literals: &Vector<Value>
    literal_map: &Map<Value, u32>

    // Line number for each instruction
    // TODO: Run-length encoding or something
    debug_locs: &Vector<DebugLocRun>
}

def Chunk::new(span: Span): &Chunk {
    let chunk = mem::alloc<Chunk>()
    chunk.start_span = span
    chunk.code = Buffer::make()
    chunk.literals = Vector<Value>::new()
    chunk.literal_map = Map<Value, u32>::new()
    chunk.debug_locs = Vector<DebugLocRun>::new()
    return chunk
}

def Chunk::span_for_offset(&this, off: u32): Span {
    let cur = 0
    for it in .debug_locs.iter() {
        cur += it.count
        if cur > off return it.span
    }
    return .start_span
}

def Chunk::push_u8(&this, val: u8, span: Span) {
    .code += val
    if .debug_locs.size > 0 and .debug_locs.back().span == span {
        .debug_locs.back_ptr().count += 1
    } else {
        .debug_locs.push(DebugLocRun(span, count: 1))
    }
}

def Chunk::push_u16(&this, val: u16, span: Span) {
    .code.write_u16(val)
    if .debug_locs.size > 0 and .debug_locs.back().span == span {
        .debug_locs.back_ptr().count += 2
    } else {
        .debug_locs.push(DebugLocRun(span, count: 2))
    }
}

def Chunk::push_op(&this, op: OpCode, span: Span) => .push_u8(op as u8, span)

def Chunk::push_with_arg_u16(&this, op: OpCode, arg: u16, span: Span) {
    .push_op(op, span)
    .push_u16(arg, span)
}

def Chunk::push_with_arg_u8(&this, op: OpCode, arg: u8, span: Span) {
    .push_op(op, span)
    .push_u8(arg, span)
}

//! Push an instruction with a literal argument. The VM is required here to be able
//! to temporarily push the literal onto the stack - this is so the garbage collector
//! can see it if it is invoked during the push. 
def Chunk::push_with_literal(&this, vm: &VM, op: OpCode, value: Value, span: Span) {
    let it = .literal_map.get_item(value)
    let idx = match it? {
        true => it.value
        false => {
            let idx = .literals.size
            assert idx < 65536, "Too many literals in a chunk"

            assert vm.stack.size + 1 < vm.stack.capacity, "Not enough space in VM stack"
            vm.stack.push(value)

            .literals.push(value)

            vm.stack.pop()

            .literal_map[value] = idx
            yield idx
        }
    }
    .push_with_arg_u16(op, idx as u16, span)
}

def Chunk::literal(&this, idx: u16): Value {
    return .literals[idx as u32]
}

def Chunk::read_u8(&this, off: &u32): u8 {
    let res = .code[*off]
    *off += 1
    return res
}

def Chunk::read_u16(&this, off: &u32): u16 {
    let res = .code[*off] as u16 << 8 | (.code[*off + 1] as u16)
    *off += 2
    return res
}

def Chunk::read_literal(&this, off: &u32): Value {
    let idx = .read_u16(off)
    return .literal(idx)
}

def Chunk::disassemble_inst(&this, off: u32, found_chunks: &Vector<&Chunk> = null): u32 {
    let op = .read_u8(&off) as OpCode // Increments off
    match op {
        Constant => { 
            let constant = .read_literal(&off)
            print("Constant: "); 
            constant.print();
            print("\n");

            if constant.is_function_code() and found_chunks? {
                let f = constant.as_function_code()
                found_chunks.push(f.chunk)
            }
        }
        Add => println("Add")
        Sub => println("Sub")
        Mul => println("Mul")
        Div => println("Div")
        Halt => println("Halt")
        LessThan => println("LessThan")
        GreaterThan => println("GreaterThan")
        Equal => println("Equal")
        SetMember | GetMember | Class | GetGlobal | SetGlobal | Method  => {
            let name = .read_literal(&off).as_obj() as &String
            println(f"{op} {name.data}")
        }
        Loop => {
            let arg = .read_u16(&off)
            println(f"Loop {arg}")
        }
        JumpIfFalse => {
            let arg = .read_u16(&off)
            println(f"JumpIfFalse {arg}")
        }
        Jump => {
            let arg = .read_u16(&off)
            println(f"Jump {arg}")
        }
        Return => println("Return")
        Call => println(f"Call {.read_u8(&off)}")
        GetLocal | SetLocal | SetUpvalue | GetUpvalue => {
            let idx = .read_u16(&off)
            println(f"{op}: {idx}")
        }
        CloseFunction => {
            let count = .read_u16(&off)
            print(f"CloseFunction [{count}]: ")
            for let i = 0u16; i < count; i += 1 {
                if i > 0 print(" ")
                print(`({.read_u8(&off) as bool}, {.read_u16(&off)})`)
            }
            print("\n")
        }
        Null => println("Null")
        Pop => println("Pop")
        True => println("True")
        False => println("False")
        CloseUpvalue => println("CloseUpvalue")
    }
    return off
}

def Chunk::dump(&this) {
    let chunks_to_dump = Vector<&Chunk>::new(capacity: 1)
    defer chunks_to_dump.free()

    println(f"=== Chunk from {.start_span.start}, size: {.code.size} ===")
    let off = 0
    while off < .code.size {
        print(f"{off:04d}: ")
        off = .disassemble_inst(off, chunks_to_dump)
    }

    for chunk in chunks_to_dump.iter() {
        chunk.dump()
    }
}

def Chunk::free(&this) {
    .code.free()
    .literals.free()
    .debug_locs.free()
    .literal_map.free()
    mem::free(this)
}