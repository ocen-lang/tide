import std::vector::{ Vector }
import std::span::{ Span }
import std::sv::{ SV }
import std::mem

import @ast::nodes::{ this, AST, Variable, Symbol }
import @errors::{ Error }
import @bytecode::{ Chunk, OpCode  }
import @vm::{ VM }
import @vm::gc
import @vm::value::{ Value, String, FunctionCode, Class }

struct LocalVar {
    name: SV
    span: Span
    depth: i32
    captured: bool
}

struct UpVar {
    idx: u16
    is_local: bool
}

struct Compiler {
    vm: &VM
    func: &FunctionCode
    chunk: &Chunk
    upvars: &Vector<UpVar>
    locals: &Vector<LocalVar>
    scope_depth: u32
    enclosing: &Compiler
}

enum CompileType {
    Script
    Function
    Method
}

def Compiler::new(
    vm: &VM, name: SV, span: Span,
    type: CompileType,
    enclosing: &Compiler = null
): &Compiler {
    let compiler = mem::alloc<Compiler>()

    let chunk = Chunk::new(span)
    let locals = Vector<LocalVar>::new(capacity: 8)
    let upvars = Vector<UpVar>::new(capacity: 256)
    let func = gc::allocate_object<FunctionCode>(FunctionCode, vm, enclosing)

    *compiler = Compiler(
        vm,
        func,
        chunk,
        upvars,
        locals,
        scope_depth: 0,
        enclosing
    )
    gc::set_compiler(compiler)

    let name_val = vm.copy_string(name.data, name.len)
    func.init(name_val.as_string(), chunk, 0) // Replace arity later...

    if type == Method {
        locals.push(LocalVar(SV("this",4), span, 0, false))
    } else {
        locals.push(LocalVar(SV("",0), span, 0, false))
    }

    return compiler
}

def Compiler::free(&this) {
    .locals.free()
    .upvars.free()
    mem::free(this)
}

def Compiler::make_str(&this, text: SV): Value {
    return .vm.copy_string(text.data, text.len)
}

def Compiler::begin_scope(&this) => .scope_depth++
def Compiler::end_scope(&this, span: Span) {
    .scope_depth--
    while .locals.size > 0 and .locals.back().depth as u32 > .scope_depth {
        let var = .locals.pop()
        if var.captured {
            .chunk.push_op(CloseUpvalue, span)
        } else {
            .chunk.push_op(Pop, span)
        }
    }
}

def Compiler::find_local(&this, name: SV, span: Span): i32 {
    for let i = 0; i < .locals.size; i++ {
        let idx = .locals.size - i - 1
        let local = .locals[idx]
        if local.name == name {
            if idx < 0 {
                Error::new(
                    span, "Compiler: Variable used before initialization."
                ).panic()
            }
            return idx as i32
        }
    }
    return -1
}

def Compiler::add_upvar(&this, idx: u16, is_local: bool, span: Span): i32 {
    for let i = 0; i < .upvars.size; i++ {
        let up = .upvars[i]
        if (up.idx == idx and up.is_local == is_local) {
            return i as i32
        }
    }

    let res = .upvars.size

    if res > 0xffff {
        Error::new(
            span, "Compiler: Too many upvalues in function."
        ).panic()
    }

    .upvars.push(UpVar(idx, is_local))
    return res as i32
}

def Compiler::find_upvar(&this, name: SV, span: Span): i32 {
    if not .enclosing? return -1

    let local_idx = .enclosing.find_local(name, span)
    if local_idx >= 0 {
        let var = .enclosing.locals.at_ptr(local_idx as u32)
        var.captured = true
        return .add_upvar(local_idx as u16, true, span)
    }

    let up_idx = .enclosing.find_upvar(name, span)
    if up_idx >= 0 {
        return .add_upvar(up_idx as u16, false, span)
    }

    return -1
}

//! Create a new variable in the current scope.
//! Meant to be called _before_ the value is pushed to the stack.
def Compiler::create_variable(&this, sym: &Symbol) {
    // Global variable
    if .scope_depth == 0 {
        return
    }

    // Local variable
    let local_var = LocalVar(sym.name, sym.span, -1, false) // Uninitialized...
    .locals.push(local_var)
}

//! Mark the last defined variable as initialized
def Compiler::mark_variable_initialized(&this) {
    if .scope_depth == 0 {
        return
    }

    let idx = .locals.size - 1
    .locals.data[idx].depth = .scope_depth as i32
}

//! Save the value on the top of the stack to last defined variable.
//! Meant to be called _after_ the value has been pushed to the stack.
def Compiler::define_variable(&this, sym: &Symbol) {
    // Local variable, nothing to do
    if .scope_depth > 0 {
        return
    }

    // Global variable
    .chunk.push_with_literal(.vm, SetGlobal, .make_str(sym.name), sym.span)
    .chunk.push_op(Pop, sym.span)
}

//! Get the value of the named variable, and push it to the stack.
def Compiler::compile_variable(&this, name: SV, span: Span) {
    let local_idx = .find_local(name, span)
    if local_idx >= 0 {
        // Local Variable
        .chunk.push_with_arg_u16(GetLocal, local_idx as u16, span)
        return
    }

    let up_idx = .find_upvar(name, span)
    if up_idx >= 0 {
        // Upvalue
        .chunk.push_with_arg_u16(GetUpvalue, up_idx as u16, span)
        return
    }

    // Global Variable
    .chunk.push_with_literal(.vm, GetGlobal, .make_str(name), span)
}

def Compiler::make_jump(&this, op: OpCode, span: Span): u32 {
    .chunk.push_with_arg_u16(op, 0xbeef, span) // Placeholder
    let patch_off = .chunk.code.size - 2
    return patch_off
}

def Compiler::make_loop(&this, target: u32, span: Span) {
    let offset = .chunk.code.size - target + 3
    if offset > 0xffff {
        Error::new(
            span, "Compiler: Loop offset too large."
        ).panic()
    }

    .chunk.push_with_arg_u16(Loop, offset as u16, span)
}

def Compiler::patch_jump(&this, from: u32, span: Span) {
    let offset = .chunk.code.size - from - 2
    if offset > 0xffff {
        Error::new(
            span, "Compiler: Jump offset too large."
        ).panic()
    }

    .chunk.code.data[from + 0] = (offset >> 8) as u8 & 0xff
    .chunk.code.data[from + 1] = offset as u8
}

def Compiler::compile_if(&this, node: &AST, is_expression: bool = false) {
    let ifs = &node.u.if_stmt
    .compile_expression(ifs.cond)

    let false_jump = .make_jump(JumpIfFalse, ifs.cond.span)
    .chunk.push_op(Pop, ifs.cond.span)
    .compile_statement(ifs.body)
    let end_jump = .make_jump(Jump, ifs.body.span)

    .patch_jump(false_jump, ifs.cond.span)
    .chunk.push_op(Pop, ifs.cond.span)
    if ifs.els? {
        .compile_statement(ifs.els)
    }
    .patch_jump(end_jump, ifs.body.span)
}

def Compiler::compile_expression(&this, node: &AST) {
    match node.type {
        IntLiteral => {
            let value = Value::Int(node.u.num_literal.text.to_i32())
            .chunk.push_with_literal(.vm, Constant, value, node.span)
        }
        FloatLiteral => {
            let value = Value::Float(std::libc::strtod(node.u.num_literal.text, null))
            .chunk.push_with_literal(.vm, Constant, value, node.span)
        }
        StringLiteral => {
            let text = node.u.string_literal
            .chunk.push_with_literal(.vm, Constant, .make_str(text), node.span)
        }
        BoolLiteral => {
            let value = Value::Bool(node.u.bool_literal)
            .chunk.push_with_literal(.vm, Constant, value, node.span)
        }
        Null => .chunk.push_op(Null, node.span)
        Identifier => .compile_variable(node.u.ident.name, node.span)
        Call => {
            let callee = node.u.call.callee

            .compile_expression(callee)

            let args = node.u.call.args
            for arg in args.iter() {
                .compile_expression(arg.expr)
            }

            .chunk.push_with_arg_u8(Call, args.size as u8, node.span)
        }
        Member => {
            let member = node.u.member
            .compile_expression(member.lhs)
            let name = .make_str(member.rhs_name)
            .chunk.push_with_literal(.vm, GetMember, name, node.span)
        }
        BinaryOp => match node.u.binary.op {
            Assignment => {
                .compile_expression(node.u.binary.rhs)

                let lhs = node.u.binary.lhs
                match lhs.type {
                    Identifier => {
                        let name = lhs.u.ident.name
                        let idx = .find_local(lhs.u.ident.name, lhs.span)
                        let up_idx = .find_upvar(name, node.span)
                        // Global variable
                        if idx >= 0 {
                            .chunk.push_with_arg_u16(SetLocal, idx as u16, node.span)

                        // Upvalue
                        } else if up_idx >= 0 {
                            .chunk.push_with_arg_u16(SetUpvalue, up_idx as u16, node.span)

                        // Local variable
                        } else {
                            .chunk.push_with_literal(.vm, SetGlobal, .make_str(name), node.span)

                        }
                    }
                    Member => {
                        let member = lhs.u.member
                        .compile_expression(member.lhs)

                        let name = .make_str(member.rhs_name)
                        .chunk.push_with_literal(.vm, SetMember, name, node.span)
                    }
                    else => {
                        Error::new(
                            lhs.span, f"Cannot assign to {lhs.type}"
                        ).panic()
                    }
                }
            }
            And => {
                .compile_expression(node.u.binary.lhs)
                let false_jump = .make_jump(JumpIfFalse, node.span)
                .chunk.push_op(Pop, node.span)
                .compile_expression(node.u.binary.rhs)
                .patch_jump(false_jump, node.span)
            }
            Or => {
                .compile_expression(node.u.binary.lhs)
                let false_jump = .make_jump(JumpIfFalse, node.span)
                let true_jump = .make_jump(Jump, node.span)
                .patch_jump(false_jump, node.span)
                .chunk.push_op(Pop, node.span)
                .compile_expression(node.u.binary.rhs)
                .patch_jump(true_jump, node.span)
            }
            // Generic Binary operators
            else => {
                .compile_expression(node.u.binary.lhs)
                .compile_expression(node.u.binary.rhs)
                match node.u.binary.op {
                    Plus => .chunk.push_op(Add, node.span),
                    Minus => .chunk.push_op(Sub, node.span),
                    Multiply => .chunk.push_op(Mul, node.span),
                    Divide => .chunk.push_op(Div, node.span),
                    LessThan => .chunk.push_op(LessThan, node.span),
                    GreaterThan => .chunk.push_op(GreaterThan, node.span),
                    Equals => .chunk.push_op(Equal, node.span),
                    else => std::panic(`Unimplemented binary operator: {node.u.binary.op}`)
                }
            }
        }
        else => {
            Error::new(
                node.span, f"Unimplemented `{node.type}` in Compiler::compile_expression"
            ).panic()
        }
    }
}

def Compiler::compile_function(&this, node: &AST, func: &nodes::Function) {
    let compile_type: CompileType = if func.is_method then Method else Function
    let cc = Compiler::new(
        .vm,
        func.sym.name,
        func.sym.span,
        compile_type,
        enclosing: this
    )
    cc.func.arity = func.params.size as u8

    cc.begin_scope()
    for param in func.params.iter() {
        cc.create_variable(param.sym)
        cc.mark_variable_initialized()
    }

    cc.compile_statement(func.body)

    // Add a placeholder return statement if none was provided
    cc.chunk.push_with_literal(.vm, Constant, Value::Null(), node.span)
    cc.chunk.push_op(Return, node.span)
    cc.end_scope(func.sym.span)

    let func_val = Value::Object(&cc.func.obj)
    .chunk.push_with_literal(.vm, Constant, func_val, node.span)

    .chunk.push_with_arg_u16(CloseFunction, cc.upvars.size as u16, node.span)
    for up in cc.upvars.iter() {
        .chunk.push_u8(up.is_local as u8, node.span)
        .chunk.push_u16(up.idx, node.span)
    }

    gc::set_compiler(this)
    cc.free()
}

def Compiler::compile_statement(&this, node: &AST) {
    match node.type {
        Block => {
            .begin_scope()

            for s in node.u.block.statements.iter() {
                .compile_statement(s)
            }

            .end_scope(node.span)
        }
        VarDeclaration => {
            let sym = node.u.var_decl.var.sym
            let expr = node.u.var_decl.init
            if expr? {
                .compile_expression(expr)
            } else {
                .chunk.push_op(Null, node.span)
            }
            .create_variable(sym)
            .define_variable(sym)
            .mark_variable_initialized()
        }
        Function => {
            let ast_func = node.u.func

            .create_variable(ast_func.sym)
            .mark_variable_initialized()

            .compile_function(node, ast_func)

            .define_variable(ast_func.sym)
        }
        Class => {
            let class = node.u.class
            let sym = class.sym
            let name = .vm.copy_string(sym.name.data, sym.name.len)
            .create_variable(sym)
            .chunk.push_with_literal(.vm, Class, name, sym.span)
            .define_variable(sym)

            // Re-get the class object
            .compile_variable(sym.name, sym.span)

            for method_ast in class.methods.iter() {
                let method = method_ast.u.func
                .compile_function(method_ast, method)
                let method_name = .make_str(method.sym.name)
                .chunk.push_with_literal(.vm, Method, method_name, method.sym.span)
            }
        }
        If => .compile_if(node, is_expression: false)
        While => {
            let loop = &node.u.loop
            let start = .chunk.code.size
            .compile_expression(loop.cond)
            let false_jump = .make_jump(JumpIfFalse, loop.cond.span)
            .chunk.push_op(Pop, loop.cond.span)

            .compile_statement(loop.body)

            .make_loop(start, loop.cond.span)
            .patch_jump(false_jump, loop.cond.span)
            .chunk.push_op(Pop, loop.cond.span)
        }
        For => {
            let loop = &node.u.loop
            .begin_scope()

            if loop.init? {
                .compile_statement(loop.init)
            }

            let start = .chunk.code.size
            if loop.cond? {
                .compile_expression(loop.cond)
            } else {
                .chunk.push_with_literal(.vm, Constant, Value::True(), node.span)
            }

            let false_jump = .make_jump(JumpIfFalse, node.span)
            .chunk.push_op(Pop, node.span)

            .compile_statement(loop.body)

            if loop.step? {
                .compile_statement(loop.step)
            }

            .make_loop(start, node.span)
            .patch_jump(false_jump, node.span)
            .chunk.push_op(Pop, node.span)

            .end_scope(node.span)
        }
        Return | ArrowReturn => {
            if node.u.child? {
                .compile_expression(node.u.child)
            } else {
                .chunk.push_op(Null, node.span)
            }
            .chunk.push_op(Return, node.span)
        }
        else => {
            .compile_expression(node)
            .chunk.push_op(Pop, node.span)
        }
    }
}

def Compiler::compile_ns(&this, root: &AST) {
    assert root.type == Namespace, "Root node must be a namespace."
    for stmt in root.u.ns.statements.iter() {
        .compile_statement(stmt)
    }
}

// TODO: More complex structures than just expressions?
def compile_program(vm: &VM, root: &AST): &FunctionCode {
    let compiler = Compiler::new(vm, SV("",0), root.span, Script)
    compiler.compile_ns(root)
    compiler.chunk.push_op(Halt, root.span)
    gc::set_compiler(null)
    let func = compiler.func
    compiler.free()
    return func
}
