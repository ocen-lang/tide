//* Entry point for the compiler

import std::fs
import std::libc::{ exit, system }
import std::vector::{ Vector }
import std::buffer::{ Buffer }
import std::span::{ Span }
import std::sv::{ SV }
import std::mem
import std::{ shift_args }
import std::logging::{ init_logging, log }

import .ast::nodes::{ * }
import .utils::{ debug }
import .opts
import .parser::{ Parser }
import .lexer::{ Lexer }
import .compiler::{ compile_program }
import .vm::{ VM }
import .vm::gc
import .vm::value::{ Value }
import .bump_alloc

def usage(code: i32, full: bool) {
    println("Usage:")
    println("   ./tide [--help] [compile-options] <file>")
    if not full then exit(code)

    println("--------------------------------------------------------")
    println("Compile Options:")
    println("    --dump-code    Dump the generated bytecode")
    println("    --trace-vm     Trace the VM execution")
    println("    --trace-stack  Trace the VM stack (implies --trace-vm)")
    println("    --trace_gc     Trace the GC execution")
    println("    --trace-alloc  Trace the memory allocation")
    println("    --gc-stats     Print GC statistics at the end of the program")
    println("    --gc-stress    Run GC after every allocation (implies --gc-stats)")
    println("    -d --debug     Debug Mode (--dump-code | --trace-vm | --trace-stack | --gc-stats)")
    println("    -h --help      Display this information")
    exit(code)
}

let filename: str = null

// @compiler c_flag "-O3 -flto"

def parse_args(argc: &i32, argv: &&str) {

    while *argc > 0 {
        let arg = shift_args(argc, argv)
        match arg {
            "--help" | "-h" => usage(code: 0, true)
            "--dump-code" => opts::dump_code = true
            "--trace-vm" => opts::trace_vm = true
            "--trace-stack" => {
                opts::trace_vm = true
                opts::trace_stack = true
            }
            "--trace-alloc" => opts::trace_allocs = true
            "--trace-gc" => opts::trace_gc = true
            "--gc-stats" => opts::gc_stats_at_exit = true
            "--gc-stress" => {
                opts::gc_stress = true
                opts::gc_stats_at_exit = true
            }
            "-d" | "--debug" => {
                opts::dump_code = true
                opts::trace_vm = true
                opts::trace_stack = true
                opts::gc_stats_at_exit = true
            }
            else => {
                if arg[0] == '-' {
                    println("Unknown option: %s", arg)
                    usage(1, true)
                } else if not filename? {
                    filename = arg
                } else {
                    println("Unknown option/argument: '%s'", arg)
                    usage(code: 1, true)
                }
            }
        }
    }

    if not filename? {
        println("No file specified")
        usage(code: 1, false)
    }
}

def main(argc: i32, argv: &str) {
    bump_alloc::initialize()

    let program_name = shift_args(&argc, &argv)
    parse_args(&argc, &argv)

    let parser = Parser::make()
    let ast = parser.parse_file(filename)
    parser.exit_with_errors_if_any()

    debug(opts::trace_allocs, f"Used {bump_alloc::cursor} bytes of memory for parsing")

    gc::initialize_gc_allocator()

    gc::state::paused = true
    let vm = VM::make()
    gc::set_vm(&vm)
    gc::state::paused = false

    let script = compile_program(&vm, ast)

    let status = vm.start_and_run(script)
    vm.free()
    std::exit(status)
}
