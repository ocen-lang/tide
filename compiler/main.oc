//* Entry point for the compiler

import std::fs
import std::libc::{ exit, system }
import std::vector::{ Vector }
import std::buffer::{ Buffer }
import std::span::{ Span }
import std::sv::{ SV }
import std::mem
import std::{ shift_args }
import std::logging::{ init_logging, log }

import .ast::nodes::{ * }
import .parser::{ Parser }
import .lexer::{ Lexer }
import .compiler::{ compile_program }
import .vm::{ VM }
import .bump_alloc

def usage(code: i32, full: bool) {
    println("Usage:")
    println("   ./ocen [--help] [compile-options] <file>")
    println("   ./ocen lsp [--help] [lsp-options] <file>")
    if not full then exit(code)

    println("--------------------------------------------------------")
    println("Compile Options:")
    println("    -o path        Output executable (default: ./out)")
    println("    -c path        Output C code (default: {out}.c)")
    println("    --no-stdlid    Don't include the standard library")
    println("    -s             Silent mode (no debug output)")
    println("    -n             Don't compile C code (default: false)")
    println("    --no-dce       Don't perform dead code elimination")
    println("    -d             Emit debug information (default: false)")
    println("    -l path        Directory to search for libraries (can be used multiple times)")
    println("    --docs path    Output documentation JSON (default: none)")
    println("    --cflags flags Additional C flags (can be used multiple times)")
    println("    -h             Display this information")
    println("    -r <args>      Run executable with arguments (can only be at the end)")
    exit(code)
}

let filename: str = null
let silent: bool = false
let debug: bool = false

// @compiler c_flag "-O3"

def parse_args(argc: &i32, argv: &&str) {

    while *argc > 0 {
        let arg = shift_args(argc, argv)
        match arg {
            "--help" => usage(code: 0, true)
            "-s" => silent = true
            "-d" => debug = true
            else => {
                if arg[0] == '-' {
                    println("Unknown option: %s", arg)
                    usage(1, true)
                } else if not filename? {
                    filename = arg
                } else {
                    println("Unknown option/argument: '%s'", arg)
                    usage(code: 1, true)
                }
            }
        }
    }

    if not filename? {
        println("No file specified")
        usage(code: 1, false)
    }

    if debug {
        init_logging(Debug, time_format: null)
    } else {
        init_logging(Info, time_format: null)
    }
}

def main(argc: i32, argv: &str) {
    bump_alloc::set_as_default()

    let program_name = shift_args(&argc, &argv)
    parse_args(&argc, &argv)

    let parser = Parser::make()
    let ast = parser.parse_file(filename)
    parser.exit_with_errors_if_any()

    log(Info, f"Used {bump_alloc::cursor} bytes of memory for parsing")
    mem::reset_to_default_allocator()

    let vm = VM::make()
    let chunk = compile_program(&vm, ast)

    if debug {
        chunk.dump()
        println("============================================")
        log(Info, "Running VM:")
    }

    let status = vm.run(chunk)

    chunk.free()
    vm.free()

    std::exit(status)
}
