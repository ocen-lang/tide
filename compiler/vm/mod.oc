import std::vector::Vector
import std::compact_map::{ Map }
import std::span::{ Location, Span }
import std::traits::hash::{ hash_bytes }
import std::mem
import std::sv::{ SV }
import std::logging::{ log }

import ..compiler::{ Compiler }
import @opts
import @errors::{ Error }
import @bytecode::{ Chunk, OpCode }
import .gc
import .value::{
    Value, String, Object, ObjectType,
    NativeFunctionType, NativeFunction, FunctionCode,
    UpValue, Function, Class, Instance, Method
}
import .native

//! Wrapper around a string that forces comparison by value, not
//! just by a pointer (like for the interned strings we use in
//! the rest of the language).
[formatting "%.*s" "$.sv.len,$.sv.data"]
struct ValueCompareString {
    sv: SV
    shash: u32
}

def ValueCompareString::hash(this): u32 => .shash
def ValueCompareString::eq(this, other: ValueCompareString): bool {
    if .shash != other.shash return false
    return .sv == other.sv
}

struct CallFrame {
    func: &Function
    chunk: &Chunk
    ip: &u8
    stack_base: u32
    is_constructor: bool
}

struct VM {
    frames: &Vector<CallFrame>

    // For the current frame
    func: &Function
    chunk: &Chunk
    ip: &u8
    stack_base: u32
    // FIXME: Instead of doing this at runtime-level, make the compiler swallow
    //        any returns inside a constructor, and directly generate the return
    //        bytecode to return the instance.
    is_constructor: bool

    cur_inst_ip: &u8

    stack: &Vector<Value>
    globals: &Map<&String, Value>
    strings: &Map<ValueCompareString, Value>
    init_string: &String

    //! Linked list of open upvalues
    open_upvalues: &UpValue
    //! Linked list of all allocated objects
    objects: &Object
    //! Used for GC tracking
    grays: &Vector<&Object>

    //! (G)arbage (C)ollector (S)tack
    //!
    //! Holds objects we want to keep alive during
    //! a GC collection, but don't semantically belong
    //! on the evaluation stack.
    gcs: &Vector<Value>
}

def VM::make(): VM {
    let vm = VM(
        frames: Vector<CallFrame>::new(),
        func: null,
        chunk: null,
        ip: null,
        stack_base: 0,
        is_constructor: false,
        cur_inst_ip: null,
        stack: Vector<Value>::new(capacity: 256),
        globals: Map<&String, Value>::new(),
        strings: Map<ValueCompareString, Value>::new(),
        init_string: null,
        open_upvalues: null,
        objects: null,
        grays: Vector<&Object>::new(),
        gcs: Vector<Value>::new(capacity: 16)
    )
    vm.init_string = vm.copy_string("init", 4).as_string()
    return vm
}

def VM::free(&this) {
    let cur = .objects

    .func = null
    .stack.clear()
    .globals.clear()
    .gcs.clear()
    .init_string = null

    gc::collect_garbage(this, null)

    .stack.free()
    .globals.free()
    .strings.free()
    .grays.free()
    .frames.free()
    .gcs.free()
    log(Info, "VM freed")
}

def VM::intern_string(&this, s: &String): Value {
    let candidate = ValueCompareString(s.sv, s.shash)
    let val = Value::Object(&s.obj)
    .gcs.push(val)
    .strings.insert(candidate, val)
    .gcs.pop()
    return val
}

def VM::take_string(&this, data: str, len: u32): Value {
    let candidate = ValueCompareString(SV(data, len), hash_bytes(data as &u8, len))
    let item = .strings.get_item(candidate)
    if item? {
        data.free()
        return item.value
    }
    let string = gc::allocate_object<String>(ObjectType::String, this)
    string.init(data, len, candidate.shash)
    return .intern_string(string)
}

//! Takes in a pointer to compiler to mark any objects that are being held
//! by the compiler. If this is being called during compile-time, then it must
//! be passed in, otherwise it can be null.
def VM::copy_string(&this, data: str, len: u32): Value {
    let candidate = ValueCompareString(SV(data, len), hash_bytes(data as &u8, len))
    let item = .strings.get_item(candidate)
    if item? return item.value

    let new_data = mem::alloc<char>(len+1) // +1 for null terminator, so we can print it
    std::libc::memcpy(new_data, data, len)
    new_data[len] = '\0'

    let string = gc::allocate_object<String>(ObjectType::String, this)
    string.init(new_data, len, candidate.shash)
    return .intern_string(string)
}

def VM::make_function(&this, code: &FunctionCode): &Function {
    let func = gc::allocate_object<Function>(ObjectType::Function, this)
    func.init(code)
    return func
}

// We don't want to allocate new memory for every string concat operation,
// Since we don't know if the resulting string is already interned or not.
const STR_CONCAT_TMP_BUF_SIZE: u32 = 1024 * 16  // 16K
let str_concat_tmp_buf: [char; STR_CONCAT_TMP_BUF_SIZE]

def VM::add(&this, a: Value, b: Value): Value => if {
    a.is_float() and b.is_float() => Value::Float(a.as_float() + b.as_float())
    a.is_float() and b.is_int() => Value::Float(a.as_float() + b.as_int() as f64)
    a.is_int() and b.is_float() => Value::Float(a.as_int() as f64 + b.as_float())
    a.is_int() and b.is_int() => Value::Int(a.as_int() + b.as_int())
    a.is_string() and b.is_string() => {
        let a_s = a.as_string().sv
        let b_s = b.as_string().sv
        // FIXME: Don't pop and push these back on, just peek the values earlier
        //        We need to do this so that the GC can find the strings when we
        //        allocate the new one (which can trigger a GC collection)
        .gcs.push(a)
        .gcs.push(b)
        let len = a_s.len + b_s.len

        import std::libc::memcpy
        let res = if len + 1 < STR_CONCAT_TMP_BUF_SIZE {
            let s = str_concat_tmp_buf
            memcpy(s         , a_s.data, a_s.len)
            memcpy(s + a_s.len, b_s.data, b_s.len)
            s[len] = '\0'
            yield .copy_string(s, len)

        } else {
            let s = mem::alloc<char>(len + 1)
            memcpy(s         , a_s.data, a_s.len)
            memcpy(s + a_s.len, b_s.data, b_s.len)
            s[len] = '\0'
            yield .take_string(s, len)
        }

        .gcs.pop() // b
        .gcs.pop() // a
        yield res
    }
    else => .error(f"Cant add {a.type_str()} and {b.type_str()}")
}

def VM::sub(&this, a: Value, b: Value): Value => if {
    a.is_float() and b.is_float() => Value::Float(a.as_float() - b.as_float())
    a.is_float() and b.is_int() => Value::Float(a.as_float() - b.as_int() as f64)
    a.is_int() and b.is_float() => Value::Float(a.as_int() as f64 - b.as_float())
    a.is_int() and b.is_int() => Value::Int(a.as_int() - b.as_int())
    else => .error(f"Cant sub {a.type_str()} and {b.type_str()}")
}

def VM::mul(&this, a: Value, b: Value): Value => if {
    a.is_float() and b.is_float() => Value::Float(a.as_float() * b.as_float())
    a.is_float() and b.is_int() => Value::Float(a.as_float() * b.as_int() as f64)
    a.is_int() and b.is_float() => Value::Float(a.as_int() as f64 * b.as_float())
    a.is_int() and b.is_int() => Value::Int(a.as_int() * b.as_int())
    else => .error(f"Cant mul {a.type_str()} and {b.type_str()}")
}

def VM::div(&this, a: Value, b: Value): Value => if {
    a.is_float() and b.is_float() => Value::Float(a.as_float() / b.as_float())
    a.is_float() and b.is_int() => Value::Float(a.as_float() / b.as_int() as f64)
    a.is_int() and b.is_float() => Value::Float(a.as_int() as f64 / b.as_float())
    a.is_int() and b.is_int() => Value::Int(a.as_int() / b.as_int())
    else => .error(f"Cant div {a.type_str()} and {b.type_str()}")
}

def VM::less_than(&this, a: Value, b: Value): Value => if {
    a.is_float() and b.is_float() => Value::Bool(a.as_float() < b.as_float())
    a.is_float() and b.is_int() => Value::Bool(a.as_float() < b.as_int() as f64)
    a.is_int() and b.is_float() => Value::Bool(a.as_int() as f64 < b.as_float())
    a.is_int() and b.is_int() => Value::Bool(a.as_int() < b.as_int())
    else => .error(f"Cant compare {a.type_str()} and {b.type_str()}")
}

def VM::greater_than(&this, a: Value, b: Value): Value => if {
    a.is_float() and b.is_float() => Value::Bool(a.as_float() > b.as_float())
    a.is_float() and b.is_int() => Value::Bool(a.as_float() > b.as_int() as f64)
    a.is_int() and b.is_float() => Value::Bool(a.as_int() as f64 > b.as_float())
    a.is_int() and b.is_int() => Value::Bool(a.as_int() > b.as_int())
    else => .error(f"Cant compare {a.type_str()} and {b.type_str()}")
}

def VM::equal(&this, a: Value, b: Value): Value {
    // Fast Path... all of these are representable as integers
    if not a.is_obj() and not b.is_obj() {
        return Value::Bool(a.u.uint == b.u.uint)
    }

    return Value::Bool(false)
}

def VM::print_value(&this, val: Value) => if {
    val.is_int() => print(`{val.as_int()}`)
    val.is_float() => print(`{val.as_float():f}`)
    val.is_bool() => print(`{val.as_bool()}`)
    val.is_null() => print("null")
    val.is_obj() => match val.as_obj().type {
        String => print(`{val.as_string().sv}`)
        Function => {
            let clos = val.as_function()
            let name = val.as_function_code().name.sv

            if {
                clos.upvalues? => print(`<func "{name}, {clos.upvalues.size} captures">`)
                name == "" => print("<script>")
                else => print(`<function "{name}">`)
            }
        }
        FunctionCode => {
            let name = val.as_function_code().name.sv
            if name == "" {
                print("<script>")
            } else {
                print(`<function "{name}">`)
            }
        }
        NativeFunction => print(`<native {val.as_native_function().name.sv}>`)
        UpValue => {
            let slot = val.as_upvalue().slot
            let upval = .stack.data[slot]
            .print_value(upval)
        }
        Class => {
            let name = val.as_class().name
            print(f"<class {name.sv}>")
        }
        Instance => {
            let name = val.as_instance().class.name
            print(f"<instance {name.sv}>")
        }
        Method => {
            let method = val.as_method()
            print(f"<method {method.func.code.name.sv}>")
        }
    }
    else => .error(`Unhandled value in VM::print(): {val.u.uint:08x}`)
}

def VM::read_u8(&this): u8 => *.ip++

def VM::read_u16(&this): u16 {
    let a = .read_u8()
    let b = .read_u8()
    return (a as u16) << 8 | (b as u16)
}

def VM::add_native_function(&this, name: str, func: NativeFunctionType) {
    let s = .copy_string(name, name.len())
    .stack.push(s)

    let native_fn = gc::allocate_object<NativeFunction>(NativeFunction, this)
    native_fn.init(s.as_string(), func)
    let value = Value::Object(&native_fn.obj)
    .stack.push(value)

    .globals.insert(s.as_string(), value)

    // Pop the function and the name
    .stack.pop()
    .stack.pop()
}

def VM::setup_native_functions(&this) {
    .add_native_function("clock", native::clock)
    .add_native_function("print", native::print)
}

def VM::run(&this, code: &FunctionCode): i32 {
    if opts::trace_vm {
        println("============================================")
        println("[VM] Starting VM execution...")
    }
    // Put the script on the `tmp` stack so the GC can find it
    .gcs.push(Value::Object(&code.obj))
    let func = .make_function(code)
    .gcs.pop()
    .stack.push(Value::Object(&func.obj))

    .func = func
    .chunk = code.chunk
    .ip = .chunk.code.data
    .stack_base = 0

    .setup_native_functions()
    return .main_loop()
}

def VM::read_literal(&this): Value => .chunk.literal(.read_u16())

def VM::is_falsey(&this, val: Value): bool => if {
    val.is_null() => true
    val.is_bool() => not val.as_bool()
    else => false  // TODO: Custom falsey values?
}

def VM::push_frame(&this, func: &Function, stack_base: u32) {
    // Push current frame, and set up the new one
    .frames.push(CallFrame(.func, .chunk, .ip, .stack_base, .is_constructor))
    .func = func
    .chunk = func.code.chunk
    .ip = .chunk.code.data
    .stack_base = stack_base
    .is_constructor = false     // We set this to true when we call a class
}

def VM::pop_frame(&this, return_val: Value) {
    // Discard all the stack variables...
    if .is_constructor {
        // If this was a constructor, return the instance
        return_val = .stack[.stack_base + 1]
    } else {
        // Otherwise, return the value
        .stack.push(return_val)
    }
    .stack.size = .stack_base

    let frame = .frames.pop()
    .func = frame.func
    .chunk = frame.chunk
    .ip = frame.ip
    .stack_base = frame.stack_base
    .is_constructor = frame.is_constructor

    .stack.push(return_val)
}

[exits]
def VM::error_at(&this, span: Span, msg: str) {
    Error::new(span, msg).panic()
}

[exits]
def VM::error(&this, msg: str) {
    let inst_off = (.cur_inst_ip - .chunk.code.data) as u32
    let span = .chunk.span_for_offset(inst_off)
    .error_at(span, msg)
}

def VM::debug_dump_inst(&this) {
    if opts::trace_stack {
        print(f"\n(base {.stack_base}):")
        for let i = 0; i < .stack.size; i++ {
            let val = .stack[i]
            print(" [")
            val.print()
            print("]")
        }
        println("")
    }

    let off = (.ip - .chunk.code.data) as u32
    .chunk.disassemble_inst(off, show_source: false)
}

def VM::capture_upvalue(&this, slot: u32): &UpValue {
    let cur = &.open_upvalues
    while (*cur)? and (*cur).slot > slot {
        (*cur) = (*cur).next
    }

    if (*cur)? and (*cur).slot == slot {
        return *cur
    }

    let upval = gc::allocate_object<UpValue>(ObjectType::UpValue, this)
    upval.init(slot)

    (*cur) = upval
    return upval
}

def VM::close_upvalue(&this, slot: u32) {
    while .open_upvalues? and .open_upvalues.slot >= slot {
        let upval = .open_upvalues
        let val = .stack.data[upval.slot]
        upval.closed = val
        upval.slot = 0
        .open_upvalues = upval.next
    }
}

def VM::bind_method(&this, func: &Function) {
    let bound = gc::allocate_object<Method>(ObjectType::Method, this)
    let instance = .stack.back()
    bound.init(instance, func)
    .stack.pop()  // Pop the instance
    .stack.push(Value::Object(&bound.obj))
}

def VM::call_function(&this, arity: u32, func: &Function) {
    if func.code.arity as u32 != arity {
        .error(f"Function {func.code.name.sv} expected {func.code.arity} arguments, got {arity}")
    }
    let val = Value::Object(&func.obj)
    .gcs.push(val)
    .push_frame(func, .stack.size - arity - 1)
    .gcs.pop()
}

// Moves the arguments on the stack to prepend the `this` value,
// then calls the function.
def VM::call_method(&this, arity: u32, func: &Function, this_val: Value) {
    // Allocate space for one more element on the stack
    .stack.push(.stack.back())
    let N = .stack.size-1
    // Move all the args up one
    for let i = 0; i < arity; i++ {
        .stack[N-i] = .stack[N-i-1]
    }
    .stack.data[N - arity] = this_val
    .call_function(arity + 1, func)
}

def VM::call_value(&this, arity: u32, val: Value) {
    if not val.is_obj() {
        .error(f"Can't call object of type {val.type_str()}")
    }

    match val.as_obj().type {
        Function => .call_function(arity, val.as_function())
        NativeFunction => {
            let func = val.as_native_function()
            let res = func.func(this, arity, &.stack.data[.stack.size - arity])
            .stack.size -= arity as u32 + 1  // Pop the arguments and the function
            .stack.push(res)
        }
        Class => {
            let class = val.as_class()
            let instance = gc::allocate_object<Instance>(ObjectType::Instance, this)
            let val = Value::Object(&instance.obj)

            // instance.init allocates memory, so we need to push this on the stack
            // so the GC can find it if we collect
            .stack.push(val)
            instance.init(class)
            .stack.pop()

            .stack.data[.stack.size - arity - 1] = val

            let res = .get_member(&class.obj, .init_string)
            if res.found {
                if not res.val.is_function() {
                    .error(f"init() must be a function, got {res.val.type_str()}")
                }
                .call_method(arity, res.val.as_function(), val)
                .is_constructor = true

            } else if arity != 0 {
                .error(f"Can't provide arguments to class without init()")
            }

        }
        Method => {
            let method = val.as_method()
            .call_method(arity, method.func, method.this_val)
        }
        FunctionCode => .error("Function should have been a func")
        else => .error(f"Can't call object of type {val.type_str()}")
    }
}

struct MemberResult {
    val: Value
    found: bool
    is_method: bool
}

def VM::get_member(&this, obj: &Object, name: &String, is_method: bool = false): MemberResult {
    // Always try accessing the object first
    let it = obj.dict.get_item(name)
    if it? {
        return MemberResult(it.value, found: true, is_method)
    }

    // If this is an instance, try the class
    if obj.type == Instance {
        let instance = obj as &Instance
        if instance.class? {
            let res = .get_member(instance.class as &Object, name, is_method: true)
            if res.found {
                return res
            }
        }
    }

    // If this is a class, try the parent
    if obj.type == Class {
        let class = obj as &Class
        if class.parent? {
            let res = .get_member(class.parent as &Object, name, is_method)
            if res.found {
                return res
            }
        }
    }

    let res: MemberResult
    res.found = false
    return res
}

def VM::main_loop(&this): i32 {
    while true {
        // FIXME: For `tests/native_fn.td`, having this line here
        //        causes perf to go from 1.2s -> 1.6s, _even if_
        //        the debug == false. Saving it into a local variable
        //        from the global one doesn't help either.
        if opts::trace_vm then .debug_dump_inst()

        .cur_inst_ip = .ip
        let op = .read_u8() as OpCode
        match op {
            Halt => return 0
            Constant => .stack.push(.read_literal())
            Add => {
                let b = .stack.pop()
                let a = .stack.pop()
                .stack.push(.add(a, b))
            }
            Sub => {
                let b = .stack.pop()
                let a = .stack.pop()
                .stack.push(.sub(a, b))
            }
            Mul => {
                let b = .stack.pop()
                let a = .stack.pop()
                .stack.push(.mul(a, b))
            }
            Div => {
                let b = .stack.pop()
                let a = .stack.pop()
                .stack.push(.div(a, b))
            }
            LessThan => {
                let b = .stack.pop()
                let a = .stack.pop()
                .stack.push(.less_than(a, b))
            }
            GreaterThan => {
                let b = .stack.pop()
                let a = .stack.pop()
                .stack.push(.greater_than(a, b))
            }
            Equal => {
                let b = .stack.pop()
                let a = .stack.pop()
                .stack.push(.equal(a, b))
            }
            JumpIfFalse => {
                let off = .read_u16()
                let val = .stack.back()  // Peek only
                if .is_falsey(val) {
                    .ip = .ip + off
                }
            }
            Jump => {
                let off = .read_u16()
                .ip = .ip + off
            }
            Loop => {
                let off = .read_u16()
                .ip = .ip - off
            }
            Pop => .stack.pop()
            Return => {
                let value = .stack.pop()

                if .frames.is_empty() {
                    // Global scope, return with the value
                    if not value.is_int() {
                        .error(f"Can only return integers from the global scope")
                    }
                    return value.as_int()

                } else {
                    .close_upvalue(.stack_base)
                    .pop_frame(value)
                }
            }
            Class => {
                let name = .read_literal().as_string()
                let class = gc::allocate_object<Class>(ObjectType::Class, this)
                .stack.push(Value::Object(&class.obj))
                class.init(name)
            }
            GetSuper => {
                let name = .read_literal().as_string()
                let super_val = .stack.pop()
                if not super_val.is_class() {
                    .error(f"Can only access super on classes, got {super_val.type_str()}")
                }
                let super_class = super_val.as_class()

                let res = .get_member(&super_class.obj, name)
                if not res.found {
                    .error(f"Method {name.sv} not found in super class")
                }

                if res.val.is_function() {
                    .bind_method(res.val.as_function())

                } else {
                    .stack.pop() // Pop the instance
                    .stack.push(res.val)
                }
            }
            AttachMethod => {
                let val = .stack.back(0)
                let class = .stack.back(1).as_class()

                let name = .read_literal().as_string()
                if not val.is_function() {
                    .error(f"Method must be a function, got {val.type_str()}")
                }
                class.obj.dict[name] = val

                .stack.pop()
            }
            Call => {
                let arity = .read_u8() as u32
                let val = .stack.back(arity as u32)
                .call_value(arity, val)
            }
            GetGlobal => {
                let name = .read_literal().as_string()
                let it = .globals.get_item(name)
                if not it? {
                    .error(f"Variable {name.sv} not found")
                }
                .stack.push(it.value)
            }
            SetGlobal => {
                let name = .read_literal().as_string()
                let value = .stack.back()
                let it = .globals.get_item(name)
                if not it? {
                    .error(f"Variable {name.sv} not found")
                }
                it.value = value
            }
            DefineGlobal => {
                let name = .read_literal().as_string()
                let value = .stack.back()
                .globals.insert(name, value)
                .stack.pop()
            }
            GetLocal => {
                let arg = .read_u16()
                let idx = arg + .stack_base as u16
                // print(`  [VM] Getting local {arg}, stack_base: {.stack_base}, idx: {idx}\n`)
                .stack.push(.stack.data[idx])
            }
            SetLocal => {
                let idx = .read_u16() + .stack_base as u16
                .stack.data[idx] = .stack.back()
            }
            SetMember => {
                let name = .read_literal().as_string()
                let obj = .stack.back(0)
                let value = .stack.back(1)
                if obj.is_immutable() {
                    .error(f"Can't set member on object of type {obj.type_str()}")
                }
                obj.as_obj().dict[name] = value
                .stack.pop()
            }
            GetMember => {
                let name = .read_literal().as_string()
                let obj = .stack.back()
                if not obj.is_obj() {
                    .error(f"Can only get members on instances, got {obj.type_str()}")
                }

                let res = .get_member(obj.as_obj(), name)
                if not res.found {
                    .error(f"Member {name.sv} not found")
                }

                if res.val.is_function() and res.is_method {
                    .bind_method(res.val.as_function())

                } else {
                    .stack.pop()  // Pop the instance
                    .stack.push(res.val)
                }
            }
            Invoke => {
                let name = .read_literal().as_string()
                let arity = .read_u8() as u32

                let obj = .stack.back(arity)
                if not obj.is_obj() {
                    .error(f"Can't access a field/member on object of type {obj.type_str()}")
                }

                let res = .get_member(obj.as_obj(), name)
                if not res.found {
                    .error(f"Could not find field/method with name {name}")
                }

                if res.val.is_function() and res.is_method {
                    .call_method(arity, res.val.as_function(), obj)

                } else {
                    .call_value(arity, res.val)
                }

            }
            Inherit => {
                let super = .stack.back(1)
                let base = .stack.back(0)

                if not super.is_class() {
                    .error(f"Can only inherit from classes, got {super.type_str()}")
                }
                let super_class = super.as_class()
                // Trust compiler that base is a class
                let base_class = base.as_class()

                base_class.parent = super_class
                .stack.pop()
            }
            Null => .stack.push(Value::Null())
            True => .stack.push(Value::True())
            False => .stack.push(Value::False())
            CloseFunction => {
                let arg = .read_literal()
                if not arg.is_function_code() {
                    .error(f"Expected function, got {arg.type_str()}")
                }
                let code = arg.as_function_code()
                .gcs.push(arg)
                let func = .make_function(code)
                let func_val = Value::Object(&func.obj)
                .gcs.pop()
                .stack.push(func_val)

                let num_upvalues = code.num_upvalues as u32
                if num_upvalues > 0 {
                    func.upvalues = Vector<&UpValue>::new(capacity: num_upvalues)
                }
                for let i = 0; i < num_upvalues; i++ {
                    let is_local = .read_u8() == 1
                    let index = .read_u16() as u32
                    if is_local {
                        let upval = .capture_upvalue(.stack_base + index)
                        func.upvalues += upval
                        // print(`  [VM] Upval {index} is local: {upval.slot} ({upval})\n`)

                    } else {
                        let upval = .func.upvalues[index]
                        func.upvalues += upval
                        // print(`  [VM] Upval {index} is outside: {upval.slot}\n`)
                    }
                }
            }
            GetUpvalue => {
                let idx = .read_u16() as u32
                let upval = .func.upvalues[idx]
                // print(`  [VM] Getting upval {idx} ({upval})\n`)
                if upval.slot == 0 {
                    .stack.push(upval.closed)
                } else {
                    .stack.push(.stack.data[upval.slot])
                }
            }
            SetUpvalue => {
                let idx = .read_u16() as u32
                let upval = .func.upvalues[idx]
                if upval.slot == 0 {
                    upval.closed = .stack.back()
                } else {
                    .stack.data[upval.slot] = .stack.back()
                }
            }
            CloseUpvalue => {
                .close_upvalue(.stack.size - 1)
                .stack.pop()
            }
        }
    }
    return 0
}
